apply plugin: 'groovy'
apply plugin: 'maven'
project.group = "com.googlecode.metridoc"
project.version = "0.1.2"

repositories {
    mavenCentral()
}

configurations {
    deployerJars
}

dependencies {
    def camelVersion = "2.9.2"
    def slf4jVersion = "1.6.2"

    deployerJars 'com.google.code.maven-svn-wagon:maven-svn-wagon:1.4'
    compile('com.google.guava:guava:13.0.1')
    compile('org.codehaus.groovy:groovy-all:2.0.5')
    testCompile('org.codehaus.groovy:groovy-all:2.0.5')
    compile("mysql:mysql-connector-java:5.1.20")
    compile("org.apache.camel:camel-core:$camelVersion") {
        exclude module: 'slf4j-api'
    }
    runtime('dom4j:dom4j:1.6.1')
    compile("org.apache.camel:camel-stream:$camelVersion") {
        exclude module: 'camel-core'
    }
    compile("org.apache.camel:camel-ftp:$camelVersion") {
        exclude module: 'camel-core'
    }
    compile('org.hibernate:hibernate-validator:4.1.0.Final') {
        exclude module: 'slf4j-api'
    }
    compile('org.hibernate:hibernate-core:3.6.10.Final') {
        exclude module: 'slf4j-api'
        exclude module: 'dom4j'
        exclude module: 'antlr'
    }
    compile("org.apache.camel:camel-groovy:$camelVersion") {
        exclude module: 'camel-core'
        exclude module: 'groovy-all'
    }
    compile("org.apache.poi:poi:3.8-beta3")
    compile("org.apache.poi:poi-ooxml:3.8-beta3") {
        exclude module: 'poi'
        exclude module: 'dom4j'
    }
    testCompile("org.slf4j:slf4j-simple:$slf4jVersion") {
        exclude module: 'slf4j-api'
    }
    compile("org.slf4j:slf4j-api:$slf4jVersion")
    compile("org.slf4j:jcl-over-slf4j:$slf4jVersion") {
        exclude module: 'slf4j-api'
    }
    compile("commons-io:commons-io:2.1")
    compile("commons-lang:commons-lang:2.4")
    testCompile group: 'junit', name: 'junit', version: '4.+'
    testCompile("org.apache.camel:camel-test:$camelVersion") {
        exclude module: "commons-logging"
        exclude module: "camel-core"
        exclude module: "spring-context"
        exclude module: "spring-aop"
        exclude module: "spring-tx"
        exclude module: "junit"
    }
    testCompile("com.h2database:h2:1.3.170")
    testCompile("org.springframework:spring-jdbc:3.1.2.RELEASE") {
        exclude module: "commons-logging"
    }
    runtime 'org.javassist:javassist:3.16.1-GA'
    runtime 'antlr:antlr:2.7.7'
    compile 'net.sf.opencsv:opencsv:2.3'
}

task wrapper(type: Wrapper) {
    gradleVersion = '1.4'
}


install {
    repositories.mavenInstaller { installer ->
        configurePom(pom)
    }
}

uploadArchives {
    repositories {
        mavenDeployer {

            repository(
                    id: "Metridoc googlecode repo",
                    url: "svn:https://metridoc.googlecode.com/svn/maven/repository",
            ) {
                authentication(userName: googlecodeUserName, password: googlecodePassword)
            }
            configurePom(pom)
            configuration = configurations.deployerJars
        }
    }
}

def configurePom(pom) {
    pom.artifactId = "metridoc-job-core"
    pom.whenConfigured { pomToConfigure ->
        pomToConfigure.dependencies.find { dep -> dep.artifactId == 'poi' }.optional = true
        pomToConfigure.dependencies.find { dep -> dep.artifactId == 'poi-ooxml' }.optional = true
        pomToConfigure.dependencies.find { dep -> dep.artifactId == 'groovy-all' }.scope = 'provided'
        def installerDelegate = delegate
        def classLoader = installerDelegate.getClass().classLoader
        def Exclusion = classLoader.loadClass("org.apache.maven.model.Exclusion")
        def slf4jExclusion = Exclusion.newInstance(
                artifactId: "slf4j-api",
                groupId: "org.slf4j"
        )

        def dom4JExclusion = Exclusion.newInstance(
                artifactId: "dom4j",
                groupId: "dom4j"
        )
        pomToConfigure.dependencies.each { dep ->
            def artifactId = dep.artifactId
            def camelCore = artifactId == 'camel-core'
            def notCoreCamelDependency = !camelCore && dep.groupId == 'org.apache.camel'
            if (notCoreCamelDependency) {
                def exclusions = dep.exclusions
                def camelCoreExclusion = Exclusion.newInstance(
                        artifactId: "camel-core",
                        groupId: "org.apache.camel"
                )

                exclusions.add(camelCoreExclusion)
            }

            if (camelCore) {
                dep.exclusions.add(slf4jExclusion)
            }

            if (artifactId == "hibernate-core" || artifactId == "hibernate-validator") {
                dep.exclusions.add(slf4jExclusion)
            }

            if (artifactId == "hibernate-core") {
                dep.exclusions.add(dom4JExclusion)
                dep.exclusions.add(Exclusion.newInstance(
                        artifactId: "antlr",
                        groupId: "antlr"
                ))
            }

            if (artifactId == "camel-groovy") {
                dep.exclusions.add(Exclusion.newInstance(
                        artifactId: "groovy-all",
                        groupId: "org.codehaus.groovy"
                ))
            }

            if (artifactId == "poi-ooxml") {
                dep.exclusions.add(Exclusion.newInstance(
                        artifactId: "poi",
                        groupId: "org.apache.poi"
                ))
                dep.exclusions.add(dom4JExclusion)
            }

            if (dep.groupId == "org.slf4j" && artifactId != "slf4j-api") {
                dep.exclusions.add(slf4jExclusion)
            }
        }
    }

    pom.project {
        name = project.description
        description = project.description
        url = 'http://metridoc.googlecode.com/'
        organization {
            name = 'Upenn Libraries'
            url = 'http://metridoc.googlecode.com/'
        }
        licenses {
            license {
                name 'Educational Community License'
                url 'http://opensource.org/licenses/ECL-2.0'
                distribution 'repo'
            }
        }
        scm {
            url = 'https://metridoc.googlecode.com/svn/trunk/metridoc-job-core/'
            connection = 'scm:svn:https://metridoc.googlecode.com/svn/trunk/metridoc-job-core/'
            developerConnection = 'scm:svn:https://metridoc.googlecode.com/svn/trunk/metridoc-job-core/'
        }
        developers {
            developer {
                id = 'tbarker'
                name = 'Thomas Barker'
                email = 'tbarker@pobox.upenn.edu'
            }
        }
    }
}




